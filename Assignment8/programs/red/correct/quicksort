function swap(ptr, i, j) {
   declare temp;
   temp := [ptr + i];
   [ptr + i] := [ptr + j];
   [ptr + j] := temp
}

function createRandomArray(max, size) {
    declare ptr;
    alloc ptr size;
    declare i;
    while(i < size) {
       [ptr + i] := random % (max + 1);
       i := i + 1
    };
    return ptr
}

function printPartition(ptr, lo, hi) {
    while(lo <= hi) {
       print("% ", [ptr + lo]);
       lo := lo + 1
    };
    print("\n")
}

function printArray(ptr, size) {
    printPartition(ptr, 0, size - 1)
}

function rng(min, max) {
    return min + random % (max + 1 - min)
}

function partition(ptr, lo, hi) {
    declare pivot;
    pivot := [ptr + rng(lo, hi)];
    declare i;
    declare j;
    i := lo - 1;
    j := hi + 1;
    while (true) {
        i := i + 1;
        while([ptr + i] < pivot) { i := i + 1 };
        j := j - 1;
        while([ptr + j] > pivot) { j := j - 1 };
        if(i >= j) { return j };
        swap(ptr, i, j)
    }
}

function quicksort(ptr, lo, hi) {
    if(lo >= hi \/ lo < 0) { return 0 };
    declare pi;
    pi := partition(ptr, lo, hi);
    quicksort(ptr, lo, pi);
    quicksort(ptr, pi + 1, hi)
}

declare max;
print("Input a maximum number in the array: ");
max := read;
declare size;
print("Input the size of the array: ");
size := read;
declare ptr;
ptr := createRandomArray(max, size);
print("The array is:\n");
printArray(ptr, size);
quicksort(ptr, 0, size - 1);
print("\nThe sorted array is: \n");
printArray(ptr, size)